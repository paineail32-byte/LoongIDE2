/*
 * LOONGARCH32 LS1C102 link script. @ 2023.6.20
 *
 * Copyright (C) 2021-2023 Suzhou Tiancheng Software Inc. All Rights Reserved.
 */

OUTPUT_FORMAT("elf32-loongarch", "elf32-loongarch", "elf32-loongarch")

OUTPUT_ARCH("loongarch")

MEMORY
{
    ROM  (rx) : ORIGIN = 0x1c000000, LENGTH = 128K
/*
    IRAM (rw) : ORIGIN = 0x80000000, LENGTH = 4K
    DRAM (rw) : ORIGIN = 0x80001000, LENGTH = 4K
 */
    DRAM (rw) : ORIGIN = 0x80000400, LENGTH = 7K
}

/*
__DRamBase = DEFINED(__DRamBase) ? __DRamBase : 0x80001000;
__DRamSize = DEFINED(__DRamSize) ? __DRamSize : 4K;
 */

__DRamBase = DEFINED(__DRamBase) ? __DRamBase : 0x80000400;
__DRamSize = DEFINED(__DRamSize) ? __DRamSize : 7K;


ENTRY(start)

SECTIONS
{
    . = 0x1C000000;
    .text :
    {
        _ftext = . ;
        *(.start)
        *(.text)
        *(.text.*)
        *(.rodata)
        *(.rodata.*)
        *(.rodata1)
        *(.rodata1.*)

        /* section information for finsh shell */
        . = ALIGN(4);
        __fsymtab_start = .;
        KEEP(*(FSymTab))
        __fsymtab_end = .;
        . = ALIGN(4);
        __vsymtab_start = .;
        KEEP(*(VSymTab))
        __vsymtab_end = .;

        . = ALIGN(4);
        __rt_init_start = .;
        KEEP(*(SORT(.rti_fn*)))
        __rt_init_end = .;

        . = ALIGN(4);
        __rt_utest_tc_tab_start = .;
        KEEP(*(UtestTcTab))
        __rt_utest_tc_tab_end = .;

        . = ALIGN(4);
        *(.got)
        . = ALIGN(4);
        *(.eh_frame)
    } > ROM

    . = ALIGN(4);
    __data_start = .;
    .data : AT(ADDR(.text) + SIZEOF(.text))
    {
         *(.data)
         *(.data.*)

         *(.data1)
         *(.data1.*)

         . = ALIGN(4);
         _gp = ABSOLUTE(.);     /* Base of small data */

         *(.sdata)
         *(.sdata.*)
    } > DRAM

    . = ALIGN(4);
    __data_end = .;
    
    . = ALIGN(4);
    .ctors :
    {
        PROVIDE(__ctors_start__ = .);
        KEEP(*(SORT(.ctors.*)))
        KEEP(*(.ctors))
        PROVIDE(__ctors_end__ = .);
    } > ROM

    . = ALIGN(4);
    .dtors :
    {
        PROVIDE(__dtors_start__ = .);
        KEEP(*(SORT(.dtors.*)))
        KEEP(*(.dtors))
        PROVIDE(__dtors_end__ = .);
    } > ROM

    . = ALIGN(4);
    .sbss :
    {
        __bss_start = .;
        *(.sbss)
        *(.sbss.*)
        *(.dynsbss)
        *(.scommon)
    } > DRAM

    . = ALIGN(4);
    .bss :
    {
        *(.bss)
        *(.bss.*)
        *(.dynbss)
        *(COMMON)
        . = ALIGN(4);
        __bss_end = .;
    } > DRAM

/*
    .stack :
    {
        . = ALIGN(4);
        _system_stack_start = .;
        . += _StackSize;
        _system_stack     = .;
    }
 */

    _end = .;
    PROVIDE(end = .);

    /* Stabs debugging sections.  */
    .stab          0 : { *(.stab) }
    .stabstr       0 : { *(.stabstr) }
    .stab.excl     0 : { *(.stab.excl) }
    .stab.exclstr  0 : { *(.stab.exclstr) }
    .stab.index    0 : { *(.stab.index) }
    .stab.indexstr 0 : { *(.stab.indexstr) }
    .comment       0 : { *(.comment) }
    /* DWARF debug sections.
     * Symbols in the DWARF debugging sections are relative to the beginning
     * of the section so we begin them at 0.  */
    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }
    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }
    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }
    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }
    /* SGI/MIPS DWARF 2 extensions */
    .debug_weaknames 0 : { *(.debug_weaknames) }
    .debug_funcnames 0 : { *(.debug_funcnames) }
    .debug_typenames 0 : { *(.debug_typenames) }
    .debug_varnames  0 : { *(.debug_varnames) }
}

/*
 * @@ END
 */

