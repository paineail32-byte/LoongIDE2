/*
 * LOONGARCH32 LS1C203 link script. @ 2025.5.31
 *
 * Copyright (C) 2021-2025 Suzhou Tiancheng Software Inc. All Rights Reserved.
 */

OUTPUT_FORMAT("elf32-loongarch", "elf32-loongarch", "elf32-loongarch")

OUTPUT_ARCH("loongarch")

MEMORY
{
    ROM  (rx)  : ORIGIN = 0x1c000000, LENGTH = 128K
    IRAM (xrw) : ORIGIN = 0x10000000, LENGTH = 8K
    DRAM (rw)  : ORIGIN = 0x10002000, LENGTH = 8K
}

__DRamBase = DEFINED(__DRamBase) ? __DRamBase : 0x10002000;
__DRamSize = DEFINED(__DRamSize) ? __DRamSize : 0x2000;

ENTRY(start)

SECTIONS
{
    . = 0x1C000000;
    .text :
    {
        *(.text)
        . = ALIGN(8);
        __iram_code_start = .;
        *(.text.iram)
        . = ALIGN(8);
        __iram_code_end = .;
        *(.rel.text)
        . = ALIGN(8);
        *(.reginfo)
        . = ALIGN(8);
        *(.ptr)
        . = ALIGN(8);
        *(.shstrtab)
        . = ALIGN(8);
        *(.strtab)
        . = ALIGN(8);
        *(.got)
        . = ALIGN(8);
        *(.eh_frame)
    } >ROM

    . = ALIGN(8);
    __rdata_start = .;          /* readonly DATA */
    .rodata :
    {
        *(.rodata)
        *(.rodata.*)
        . = ALIGN(8);
        __rdata_end = .;        /* rodata end */
    } >ROM

    . = ALIGN(8);
    __data_start = .;           /* initialized DATA */
    .data : AT(ADDR(.rodata) + SIZEOF(.rodata))
    {
        *(.data)
    } > DRAM

    _gp = ABSOLUTE(.);          /* TODO ? */
    __data_end = .;

    . = ALIGN(8);
    __bss_start = .;            /* BSS start */
    .sbss :
    {
        *(.sbss)
    } > DRAM

    . = ALIGN(8);
    .bss :
    {
        *(.bss)
    } > DRAM

    . = ALIGN(8);
    .scommon :
    {
        *(.scommon)
    } > DRAM
    
    . = ALIGN(8);
    __bss_end = .;              /* BSS ending */
    
}

/*
 * @@ END
 */
 
